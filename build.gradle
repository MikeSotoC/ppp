plugins {
    /* Beekeeper */
    id 'io.beekeeper.gradle.plugins.dependency-updates' version '0.9.14'
    id 'io.beekeeper.gradle.plugins.formatter' version '0.9.14'
    id 'io.beekeeper.gradle.plugins.ide' version '0.9.14'
    /* General */
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java-library'
    id 'checkstyle'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
}

allprojects {
    group 'com.heretere'
    version '1.0.1'
    description "Activate plugins in specific worlds."

    tasks.withType(Checkstyle) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }
}

dependencies {
    annotationProcessor 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
    compileOnly 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'

    compileOnlyApi 'org.jetbrains:annotations:20.1.0'
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'

    implementation 'com.heretere.hdl:hdl-spigot:1.5.0'
    compileOnly 'com.github.heretere:hch:v1.1.0'

    /* ACF */
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
}

task replaceSources(type: Sync) {
    inputs.property 'version', version
    from sourceSets.main.java
    into "$buildDir/src"
    filter { line -> line.replaceAll('VERSION', version) }
}

compileJava {
    dependsOn spotlessApply
    source = replaceSources.outputs
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

shadowJar {
    archiveFileName = 'HPWP.jar'
    relocate 'com.heretere.hch', 'com.heretere.hpwp.libs.hch'
    relocate 'com.heretere.hdl', 'com.heretere.hpwp.libs.hdl'
    relocate 'org.antlr', 'com.heretere.hpwp.libs.hdl.antlr'
    relocate 'org.tomlj', 'com.heretere.hpwp.libs.hdl.tomlj'
    relocate 'co.aikar.commands', 'com.heretere.hpwp.libs.acf'
    relocate 'co.aikar.locales', 'com.heretere.hpwp.libs.acf.locales'
}
