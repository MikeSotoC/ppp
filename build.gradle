plugins {
    /* Beekeeper */
    id 'io.beekeeper.gradle.plugins.dependency-updates' version '0.9.14'
    id 'io.beekeeper.gradle.plugins.formatter' version '0.9.14'
    id 'io.beekeeper.gradle.plugins.ide' version '0.9.14'
    /* General */
    id 'base'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java-library'
    id 'checkstyle'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url 'https://repo.minebench.de/' }
}

allprojects {
    group 'com.heretere'
    version '1.2.2'
    description "Activate plugins in specific worlds."

    tasks.withType(Checkstyle) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }
}

dependencies {
    annotationProcessor 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
    compileOnly 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'

    compileOnlyApi 'org.jetbrains:annotations:20.1.0'
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'

    implementation 'de.themoep:inventorygui:1.4.3-SNAPSHOT'

    implementation 'com.heretere.hch:hch-spigot-yaml:1.0.0'

    implementation 'com.github.cryptomorin:XSeries:7.8.0'

    /* ACF */
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
}

task replaceSources(type: Sync) {
    inputs.property 'version', version
    from sourceSets.main.java
    into "$buildDir/src"
    filter { line -> line.replaceAll('VERSION', version) }
}

compileJava {
    dependsOn spotlessApply
    source = replaceSources.outputs
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

shadowJar {
    archiveFileName = 'HPWP.jar'
    relocate 'com.cryptomorin.xseries', 'com.heretere.hpwp.libs.xseries'
    relocate 'de.themoep.inventorygui', 'com.heretere.hpwp.libs.inventory'
    relocate 'com.heretere.hch', 'com.heretere.hpwp.libs.hch'
    relocate 'co.aikar.commands', 'com.heretere.hpwp.libs.acf'
    relocate 'co.aikar.locales', 'com.heretere.hpwp.libs.acf.locales'
    minimize()
}
