plugins {
    /* Beekeeper */
    id 'io.beekeeper.gradle.plugins.formatter' version '0.9.18'
    id 'io.beekeeper.gradle.plugins.ide' version '0.9.18'
    /* General */
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'java-library'
    id 'io.freefair.lombok' version '6.0.0-m2'
    id 'com.github.ben-manes.versions' version '0.39.0'
}

repositories {
    mavenCentral()
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url 'https://repo.minebench.de/' }
    maven { url 'https://repo.codemc.io/repository/maven-snapshots/' }
    maven { url 'https://repo.codemc.io/repository/nms/' }
}

allprojects {
    group 'com.heretere'
    version '1.4.1'
    description "Activate plugins in specific worlds."
}

dependencies {
    annotationProcessor 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
    compileOnly 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'

    compileOnlyApi 'org.jetbrains:annotations:21.0.1'
    compileOnly 'org.spigotmc:spigot:1.17-R0.1-SNAPSHOT'

    implementation 'org.bstats:bstats-bukkit:2.2.1'

    implementation 'de.themoep:inventorygui:1.5-SNAPSHOT'

    implementation 'com.github.cryptomorin:XSeries:8.2.0'

    implementation 'net.wesjd:anvilgui:1.5.1-SNAPSHOT'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    /* ACF */
    implementation 'co.aikar:taskchain-bukkit:3.7.2'
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
}

task replaceSources(type: Sync) {
    inputs.property 'version', version
    from sourceSets.main.java
    into "$buildDir/src"
    filter { line -> line.replaceAll('VERSION', version) }
    filter { line -> line.replaceAll('DISTRIBUTION', 'self-compiled') }
}

compileJava {
    dependsOn spotlessApply
    source = replaceSources.outputs
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

shadowJar {
    archiveFileName = 'HPWP.jar'
    relocate 'com.cryptomorin.xseries', 'com.heretere.hpwp.libs.xseries'
    relocate 'de.themoep.inventorygui', 'com.heretere.hpwp.libs.inventory'
    relocate 'com.heretere.hch', 'com.heretere.hpwp.libs.hch'
    relocate 'co.aikar.taskchain', 'com.heretere.hpwp.libs.taskchain'
    relocate 'co.aikar.commands', 'com.heretere.hpwp.libs.acf'
    relocate 'co.aikar.locales', 'com.heretere.hpwp.libs.acf.locales'
    relocate 'org.bstats', 'com.heretere.hpwp.libs.bstats'
    minimize()
}
